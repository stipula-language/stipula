stipula License {
assets balance, token
fields t_start, t_limit, cost, code
init Inactive

agreement (Licensor, Licensee)(t_start, t_limit, cost){
		Licensor , Licensee : t_start, t_limit, cost
} => @Inactive

@Inactive Licensor : offerLicense (x)[n]{
    n -o token
    x -> code
    now + t_start >> @Proposal{
        token -o Licensor
        } => @End
} => @Proposal

@Proposal Licensee : activateLicense()[b] (b == cost) {
    b -o balance
    code -> Licensee
    now + t_limit >> @Trial {
        balance -o Licensee
        token -o Licensor
        } => @End
} => @Trial

@Trial Licensee : buy ()[]{
    balance -o Licensor
    token -o Licensee
} => @End
}